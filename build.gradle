plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.+'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven{
        url = "https://cursemaven.com"
    }
    maven {
        name = "Valkyrien Skies"
        url = "https://maven.valkyrienskies.org"
    }
}

base {
    archivesName = mod_id+"-1.20.1-Forge"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

legacyForge {
    // Specify the version of NeoForge to use.
    version = "1.20.1-47.3.0"

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }


dependencies {

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    modCompileOnlyApi "curse.maven:oculus-581495:6020952"
    //implementation(files("libs/satin-1.0.0.jar"))
    modCompileOnlyApi "curse.maven:embeddium-908741:5681725"
    modImplementation "curse.maven:valkyrien-skies-258371:7178865"
    modImplementation "curse.maven:kotlin-for-forge-351264:5402061"
    modImplementation "curse.maven:alekiships-1068445:5963449"
    modImplementation"curse.maven:eureka-ships-654384:5321630"

    compileOnly("org.valkyrienskies:valkyrienskies-120-forge:${vs2_version}") {
        exclude group: 'com.simibubi', module: ''
        exclude group: 'dev.engine-room', module: ''
        exclude group: 'com.jozufozu', module: ''
    }
    compileOnly("org.valkyrienskies.core:api:${vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    compileOnly("org.valkyrienskies.core:api-game:${vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    compileOnly("org.valkyrienskies.core:util:${vscore_version}") {
        exclude group: 'org.joml', module: ''
    }
    compileOnly 'org.joml:joml-primitives:1.10.0'
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
}

jar {
    manifest.attributes([
            "MixinConfigs": "wakes.mixins.json"
    ])
}


// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
legacyForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
